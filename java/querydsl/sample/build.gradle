apply plugin: 'java'
apply plugin: 'application'

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.mysema.querydsl:querydsl-sql-codegen:3.6.0'
		classpath 'mysql:mysql-connector-java:5.1.34'
	}
}

repositories {
	jcenter()
}

dependencies {
	compile 'com.mysema.querydsl:querydsl-sql:3.6.0'
	runtime 'mysql:mysql-connector-java:5.1.34'
}

mainClassName = 'sample.SampleApp'

task generate << {
	def con = new com.mysql.jdbc.Driver().connect('jdbc:mysql://localhost:3306/sample?user=root', null)

	def exporter = new com.mysema.query.sql.codegen.MetaDataExporter()

	def conf = new com.mysema.query.sql.Configuration(
		new com.mysema.query.sql.MySQLTemplates()
	)

	conf.register('product', 'price', BigDecimal)

	exporter.setConfiguration(conf)

	exporter.packageName = 'sample.model'
	exporter.targetFolder = new File('src/main/java')

	exporter.export(con.metaData)

	con.close()
}
