apply plugin: 'java'

def enc = 'UTF-8'
tasks.withType(AbstractCompile)*.options*.encoding = enc

def qdslDestDir = 'src/main/qdsl-generated'

repositories {
	jcenter()
}

configurations {
	apt
}

dependencies {
	apt 'com.mysema.querydsl:querydsl-apt:3.6.2'

	compile 'com.mysema.querydsl:querydsl-jpa:3.6.2'
	compile 'javax:javaee-api:7.0'
}

task generate(type: JavaCompile) {
	def destDir = new File(qdslDestDir)
	destDir.mkdirs()

	source = sourceSets.main.java
	classpath = configurations.compile + configurations.apt

	options.compilerArgs += [
		'-proc:only', 
		'-processor', 'com.mysema.query.apt.jpa.JPAAnnotationProcessor'
	]

	destinationDir = destDir
}

compileJava {
	dependsOn generate
	sourceSets.main.java.srcDir qdslDestDir
}

clean {
	delete qdslDestDir
}
