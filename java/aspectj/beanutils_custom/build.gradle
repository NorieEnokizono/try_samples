apply plugin: 'java'

def aspectjVersion = '1.8.0'
def trgModuleId = 'commons-beanutils'
def trgModule = "commons-beanutils:${trgModuleId}:1.8.3"

repositories {
	mavenCentral()
}

configurations {
	ajc
}

dependencies {
	ajc "org.aspectj:aspectjtools:${aspectjVersion}"
	compile "org.aspectj:aspectjrt:${aspectjVersion}"
	compile trgModule
	compile 'commons-collections:commons-collections:3.2.1'
}

task ajc << {
	ant.taskdef(resource: 'org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties', classpath: configurations.ajc.asPath) {
	}

	ant.iajc(outJar: "${trgModuleId}_custom.jar", source: '1.7', showWeaveInfo: 'true') {
		sourceroots {
			sourceSets.main.java.srcDirs.each {
				pathelement(location: it.absolutePath)
			}
		}
		classpath {
			pathelement(location: configurations.compile.asPath)
		}
		inpath {
			pathelement(location: configurations.compile.files.find { it.path.contains trgModuleId }.absolutePath)
		}
	}
}
