
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'com.google.protobuf' version '0.8.8'
    id 'application'
}

repositories {
    jcenter()
}

def grpcVersion = '1.20.0'

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"

    compileOnly "javax.annotation:javax.annotation-api:1.3.2"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.7.1' }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }

    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

startScripts.enabled = false

task server(type: CreateStartScripts) {
    mainClassName = 'sample.server.ServerAppKt'
    applicationName = 'sample-server'
    outputDir = file("${project.buildDir}/bin")
    classpath = startScripts.classpath
}

task client(type: CreateStartScripts) {
    mainClassName = 'sample.client.ClientAppKt'
    applicationName = 'sample-client'
    outputDir = file("${project.buildDir}/bin")
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(server)
    from(client)
}
